project.ext {
  set('react-native', [
    versions: [
      android           : [
        minSdk    : 16,
        targetSdk : 28,
        compileSdk: 28,
        // optional as gradle.buildTools comes with one by default
        // overriding here though to match the version RN uses
        buildTools: "28.0.3"
      ],

      googlePlayServices: [
        base: "16.0.1",
      ],

      firebase          : [
        analytics: "16.0.6"
      ],
    ],
  ])
}

buildscript {
  repositories {
    google()
    jcenter()
    maven {
      url uri('/Volumes/Projects/Projects/react-native-gradle-plugin/repo')
    }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.2.1'
    classpath 'io.invertase.gradle:react-native:1.1.1'
  }
}

apply plugin: "io.invertase.gradle.build"

android {
  defaultConfig {
    multiDexEnabled true
  }
  lintOptions {
    disable 'GradleCompatible'
    abortOnError false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  google()
  jcenter()
}

dependencies {
  api project(':@react-native-firebase/app')
  implementation "com.google.firebase:firebase-analytics:${ReactNative.ext.getVersion("firebase", "analytics")}"
  implementation "com.google.android.gms:play-services-base:${ReactNative.ext.getVersion("googlePlayServices", "base")}"
}

ReactNative.shared.applyPackageVersion()
ReactNative.shared.applyDefaultExcludes()
ReactNative.module.applyAndroidVersions()
ReactNative.module.applyReactNativeDependency("api")
