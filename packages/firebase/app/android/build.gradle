apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

def DEFAULT_TARGET_SDK_VERSION = 28
def DEFAULT_COMPILE_SDK_VERSION = 28
def DEFAULT_KOTLIN_VERSION = "1.3.11"
def DEFAULT_BUILD_TOOLS_VERSION = "28.0.2"
def KOTLIN_VERSION = rootProject.hasProperty('kotlinVersion') ? rootProject.kotlinVersion : DEFAULT_KOTLIN_VERSION

android {
  compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
  buildToolsVersion rootProject.hasProperty('buildToolsVersion') ? rootProject.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION
  defaultConfig {
    minSdkVersion 16
    targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
    versionCode 1
    versionName "1.0"
    multiDexEnabled true
  }
  lintOptions {
    disable 'GradleCompatible'
    abortOnError false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  packagingOptions {
    exclude 'META-INF/-no-jdk.kotlin_module'
  }
}

buildscript {
  repositories {
    google()
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.2.1'
  }
}

repositories {
  google()
  jcenter()
}

dependencies {
  /* -----------------
   *     FIREBASE
   * ----------------- */
  // Required dependencies
  // noinspection GradleCompatible
  implementation "com.google.firebase:firebase-core:16.0.6"
  implementation "com.google.android.gms:play-services-base:16.0.1"
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$KOTLIN_VERSION"
}

// Flutter
if (new File(rootProject.projectDir.parentFile, 'pubspec.yaml').exists() && findProject(':expo-core') == null) {
  throw new GradleException("Support for Flutter has not been implemented.")
// Expo
} else if (findProject(':expo-core') != null) {
  apply from: file("./expo.gradle")
// React Native
} else if (new File(rootProject.projectDir.parentFile, 'package.json').exists()) {
  apply from: file("./react-native.gradle")
} else {
  throw new GradleException("Unknown Project Configuration, please report this issue on GitHub.")
}
